VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "Sheet1"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Option Explicit
Private Const MODULE_NAME As String = "main"

'------------------------------------------------
' シート名
'------------------------------------------------
' メイン
Private Const SHEET_MAIN As String = "main"
' Grep結果テンプレート
Private Const SHEET_RESULT_TMPL As String = "result_template"
' Grep結果テンプレート(2カラム)
Private Const SHEET_RESULT_TMPL_TWO_COLUMN As String = "result_template2"

'------------------------------------------------
' セルのアドレス
'------------------------------------------------
' 検索キーワード
Private Const CELL_KEYWORD As String = "main!B1"
' 検索対象のファイルパス
Private Const CELL_FILEPATH_OR_DIRPATH As String = "main!B2"
' 正規表現の使用有無
Private Const CELL_IS_REGEXP As String = "main!B2"

'------------------------------------------------
' Grep結果シートのセルのアドレス
'------------------------------------------------
' Grep結果を代入する基点行
Private Const RESULT_SHEET_ROOT_ROW As Integer = 8
' Grep結果を代入する基点列
Private Const RESULT_SHEET_ROOT_COLUMN As Integer = 1
' Grep結果を代入する基点列
Private Const RESULT_SHEET_ROOT_COLUMN2 As Integer = 7

'------------------------------------------------
' チェックボックス名
'------------------------------------------------
' 大文字小文字を区別するか
Private Const CHKBOX_IS_IGNORE_CASE As String = "CHKBOX_IS_IGNORE_CASE"
' 正規表現を使用するか
Private Const CHKBOX_IS_USE_REGEXP As String = "CHKBOX_IS_USE_REGEXP"
' サブフォルダを検索するか
Private Const CHKBOX_IS_RECURSIVE_SEARCH As String = "CHKBOX_IS_RECURSIVE_SEARCH"

' Grep結果を表示するテキストボックス
Private Const SHAPE_RESULT_TXT_BOX As String = "RESULT_TXT_BOX"

' Grep結果の件数が多い場合に警告表示する件数
Private Const WARNING_ALERT_COUNT As Long = 10000

' VBAのInteger最大値
Private Const VBA_INTEGER_MAX As Integer = 32767

'
' 「検索ボタン」押下時のイベント処理を行います.
'
Private Sub CommandButton1_Click()
    On Error GoTo ErrHandler

    Dim keyword As String
    Dim filePathOrDirPath As String
    Dim isIgnoreCase As Boolean
    Dim isUseRegExp As Boolean
    Dim isRecursiveSearch As Boolean

    Dim filePaths As Collection
    Dim resultList As Collection

    Dim yesNo As Integer

    ' 入力値を取得
    keyword = Range(CELL_KEYWORD).Value
    filePathOrDirPath = Range(CELL_FILEPATH_OR_DIRPATH).Value
    isIgnoreCase = Not GetCheckBoxValue(SHEET_MAIN, CHKBOX_IS_IGNORE_CASE)
    isUseRegExp = GetCheckBoxValue(SHEET_MAIN, CHKBOX_IS_USE_REGEXP)
    isRecursiveSearch = GetCheckBoxValue(SHEET_MAIN, CHKBOX_IS_RECURSIVE_SEARCH)

    ' ファイル一覧を取得
    Set filePaths = GetFilePaths(filePathOrDirPath, isRecursiveSearch)

    If filePaths.count = 0 Then
        MsgBox "検索対象のファイルは0件です。", vbOKOnly + vbCritical, ""
        Exit Sub
    End If

    ' Grep結果を取得
    Set resultList = Grep(keyword, filePaths, isIgnoreCase, isUseRegExp)

    If resultList.count = 0 Then
        MsgBox "該当のファイルは0件です。", vbOKOnly + vbCritical, ""
        Exit Sub
    End If

    If resultList.count >= WARNING_ALERT_COUNT Then
        yesNo = MsgBox("検索結果件数が1万件を超えますが、処理を継続しますか？" & vbCrLf & "処理件数：" & resultList.count, vbYesNo + vbExclamation, "")
        If yesNo <> vbYes Then
            MsgBox "処理を中断しました。"
            Exit Sub
        End If
    End If

    WriteResult resultList, keyword, filePathOrDirPath, isIgnoreCase, isUseRegExp, isRecursiveSearch

    MsgBox resultList.count & "件見つかりました。", vbOKOnly, ""

    Exit Sub
ErrHandler:
    MsgBox "ファイル検索中にエラーが発生しました。" & vbCrLf & Err.Description, vbOKOnly + vbCritical, ""
    Debug.Print "Source: " & Err.Source & vbCrLf & _
                "Description: " & Err.Description & vbCrLf & _
                "Number: " & Err.Number
End Sub

'
' 検索対象のファイルパスリストを取得します.
'
' @param filePathOrDirPath 検索対象のファイルパス/ディレクトリパス
' @param isRecursiveSearch サブディレクトリも再帰的に検索するか
' @result Grep結果リスト
'
Private Function GetFilePaths(ByVal filePathOrDirPath As String, ByVal isRecursiveSearch As Boolean) As Collection
    On Error GoTo ErrHandler

    Dim fso As FileSystemObject
    Dim fsoex As FileSystemObjectExtract
    Dim fileType As String
    Dim filePaths As Collection
    Dim extensions As Collection

    ' 検索対象のファイル一覧を取得
    Set fso = New FileSystemObject
    Set fsoex = New FileSystemObjectExtract
    fsoex.Initialize fso

    ' ファイルかディレクトリか判定
    fileType = fsoex.GetFilePathType(filePathOrDirPath)

    ' ファイルの場合は1ファイルのみ検索を行う
    If fileType = FILE_PATH_TYPE_FILE Then
        Set filePaths = New Collection
        filePaths.Add filePathOrDirPath
        ' ディレクトリの場合は、フォルダ配下のExcelファイル全てを対象に検索を行う
    ElseIf fileType = FILE_PATH_TYPE_DIRECTORY Then
        Set extensions = New Collection
        extensions.Add ("xls")
        extensions.Add ("xlsx")
        Set filePaths = fsoex.GetFilePaths(filePathOrDirPath, extensions, isRecursiveSearch)
    Else
        MsgBox filePathOrDirPath & "は存在しません。", vbOKOnly + vbCritical, ""
        Exit Function
    End If

    Set GetFilePaths = filePaths
    Exit Function
ErrHandler:
    ThrowError Err, MODULE_NAME, "GetFilePaths"
End Function

'
' Grep検索を実行し、検索結果を返します.
'
' @param keyword 検索キーワード
' @param filePaths 検索対象のファイルパスリスト
' @param isIgnoreCase 大文字小文字を区別するか
' @param isUseRegExp 正規表現の使用有無
' @result Grep結果リスト
'
Private Function Grep(ByRef keyword As String, ByRef filePaths As Collection, ByVal isIgnoreCase As Boolean, ByVal isUseRegExp As Boolean) As Collection
    On Error GoTo ErrHandler

    Dim greper As ExcelGreper
    Set greper = New ExcelGreper

    greper.Initialize keyword, filePaths, isIgnoreCase, isUseRegExp
    Set Grep = greper.Execute()

    Exit Function
ErrHandler:
    ThrowError Err, MODULE_NAME, "Grep"
End Function

'
' Grep結果を別シートに出力します.
'
' @param resultList Grep結果リスト
' @param keyword 検索キーワード
' @param filePath Grepを行ったファイルのパス
'
Private Sub WriteResult(ByRef resultList As Collection, _
                        ByRef keyword As String, _
                        ByRef filePathOrDirPath As String, _
                        ByVal isIgnoreCase As Boolean, _
                        ByVal isUseRegExp As Boolean, _
                        ByVal isRecursiveSearch As Boolean)
    On Error GoTo ErrHandler
    
    Dim copyedSheet As Worksheet
    Dim resultTxtBox As shape
    Dim resultTxt As String
    
    Dim useTmpl2 As Boolean
    Dim tmplSheetName As String

    If resultList.count > VBA_INTEGER_MAX Then
        useTmpl2 = True
        tmplSheetName = SHEET_RESULT_TMPL_TWO_COLUMN
    Else
        tmplSheetName = SHEET_RESULT_TMPL
    End If
    
    ' テンプレートのシートをコピー
    ThisWorkbook.Worksheets(tmplSheetName).Copy after:=ThisWorkbook.Worksheets(Worksheets.count)
    Set copyedSheet = ThisWorkbook.Worksheets(Worksheets.count)
    copyedSheet.Name = Format(Now, "yyyymmdd_hhmmss") & "_result"

    ' Grep結果をテキストボックスのテキストへ代入
    resultTxt = "Grepキーワード : " & keyword & vbCrLf
    resultTxt = resultTxt & "ファイル/ディレクトリパス    : " & filePathOrDirPath & vbCrLf

    If Not isIgnoreCase Then
        resultTxt = resultTxt & "大文字小文字を区別する" & vbCrLf
    End If

    If isUseRegExp Then
        resultTxt = resultTxt & "正規表現を使用する" & vbCrLf
    End If

    If isRecursiveSearch Then
        resultTxt = resultTxt & "サブフォルダも検索する" & vbCrLf
    End If

    resultTxt = resultTxt & vbCrLf & resultList.count & "件見つかりました｡"
    Set resultTxtBox = copyedSheet.Shapes(SHAPE_RESULT_TXT_BOX)
    resultTxtBox.TextFrame.Characters.Text = resultTxt

    '
    If useTmpl2 Then
        Dim resultArray1() As String
        Dim resultArray2() As String
                            
        resultArray1 = ToArray(resultList, _
                               1, _
                               Rows.count - RESULT_SHEET_ROOT_ROW + 1, _
                               GREP_RESULT_COLUMN_COUNT)
        
        PrintResult copyedSheet, resultArray1, _
                    RESULT_SHEET_ROOT_ROW, _
                    RESULT_SHEET_ROOT_COLUMN, _
                    Rows.count, _
                    GREP_RESULT_COLUMN_COUNT
                    
        resultArray2 = ToArray(resultList, _
                               Rows.count - RESULT_SHEET_ROOT_ROW + 2, _
                               resultList.count, _
                               GREP_RESULT_COLUMN_COUNT)
        
        PrintResult copyedSheet, resultArray2, _
                    RESULT_SHEET_ROOT_ROW, _
                    RESULT_SHEET_ROOT_COLUMN2, _
                    resultList.count - Rows.count + RESULT_SHEET_ROOT_ROW + RESULT_SHEET_ROOT_ROW - 2, _
                    RESULT_SHEET_ROOT_COLUMN2 + GREP_RESULT_COLUMN_COUNT - 1
    Else
        Dim resultArray() As String
        resultArray = ToArray(resultList, _
                              1, _
                              resultList.count, _
                              GREP_RESULT_COLUMN_COUNT)
                              
        PrintResult copyedSheet, resultArray, _
                    RESULT_SHEET_ROOT_ROW, _
                    RESULT_SHEET_ROOT_COLUMN, _
                    RESULT_SHEET_ROOT_ROW + resultList.count - 1, _
                    GREP_RESULT_COLUMN_COUNT
    End If

    copyedSheet.Visible = xlSheetVisible
    Exit Sub
ErrHandler:
    ' エラー起きたらコピーしたシートを削除する
    Dim sh As Worksheet
    If Not copyedSheet Is Nothing Then
        For Each sh In ThisWorkbook.Worksheets
            If sh.Name = copyedSheet.Name Then
                Application.DisplayAlerts = False
                copyedSheet.Delete
                Application.DisplayAlerts = True
            End If
        Next
    End If
    ThrowError Err, MODULE_NAME, "WriteResult"
End Sub

Private Function ToArray(ByRef resultList As Collection, _
                    ByVal startIdx As Long, _
                    ByVal endIdx As Long, _
                    ByVal colCount As Long) As String()
                    
    Dim result As GrepResult
    Dim tmpArr() As String
    Dim i As Long
    Dim objName As String
    
    Dim idx As Long
    idx = 0
    
    ' Grep結果リストを2次元配列へ変換
    ReDim tmpArr(endIdx - startIdx, colCount - 1)
    For i = startIdx To endIdx
        objName = ObjectTypeToName(resultList(i).objectType)

        tmpArr(idx, 0) = Str(i)
        tmpArr(idx, 1) = resultList(i).FilePath
        tmpArr(idx, 2) = objName
        tmpArr(idx, 3) = resultList(i).CellAddress
        tmpArr(idx, 4) = resultList(i).Text
        
        idx = idx + 1
    Next
    
    ToArray = tmpArr
    
    Exit Function
ErrHandler:
    ThrowError Err, MODULE_NAME, "ToArray"
End Function

Private Sub PrintResult(ByRef printSheet As Worksheet, _
                              ByRef resultArray() As String, _
                              ByRef sRow As Long, _
                              ByRef sCol As Long, _
                              ByRef eRow As Long, _
                              ByRef eCol As Long)

    Dim printRngStr As String
    Dim printRng As Range
    
' Grep結果を代入するセル範囲を取得
    With printSheet
        Set printRng = _
        .Range( _
            .Cells(sRow, sCol), _
            .Cells(eRow, eCol))
    End With

    ' セルへ一括代入
    printRng = resultArray

    ' 罫線を描画
    printRng.Borders.LineStyle = xlContinuous
    printSheet.Columns("A:E").EntireColumn.AutoFit
    Exit Sub
ErrHandler:
    ThrowError Err, MODULE_NAME, "PrintResult"
End Sub
